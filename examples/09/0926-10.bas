10 MAXFILES=3
20 OPEN "экзамен"AS#1 LEN=39
30 FIELD #1, 15 AS I$,  10 AS T1$,  2 AS O1$,  10 AS T2$,  2 AS O2$
31 '                ▌          ▌          ▌           ▌          ▌
32 '                V          V          V           V          V
33 '         Фамилия,имя    Первая     Балл по     Вторая     Балл по
34 '                         тема    первой теме    тема    второй теме
40 OPEN "Фамилия"AS#2 LEN=15
50 FIELD #2,15 AS F$     'Фамилия ученика, у которого средний балл  по двум темам ╡ 4
60 OPEN "особенное"AS#3 LEN=15
70 FIELD #3,15 AS F1$    'Фамилия ученика, у которого хотя бы по одной теме балл 4 или 5
75 INPUT "Введите количество учащихся";N
80 FOR I=1 TO N
90     INPUT "Ф.И.";D$:LSET I$=D$:INPUT "Тема1";D$:LSET T1$=D$:INPUT"Балл";D%:LSET O1$=MKI$(D%):INPUT "Тема2";D$:LSET T2$=D$:INPUT "Балл";D%:LSET O2$=MKI$(D%):PUT#1,I
110 NEXT:A=0       'A - количество учеников, у которых средний балл по двум темам ╡ 4
120 FOR K=1 TO N:GET#1,K                          'Заполнение файла #2
125     L=(CVI(O1$)+CVI(O2$))/2
130     IF L>=4 THEN LSET F$=I$:A=A+1:PUT #2,A
140 NEXT K
146 PRINT"Ф.И. хорошистов и отличников:"
150 FOR I=1 TO A:GET #2,I:PRINT F$:NEXT I         'Вывод на экран
170 B=0            'B - количество учеников,у которых хотя бы по одной теме балл 4 или 5
175 FOR K=1 TO N                                  'Заполнение файла #3
177     GET #1,K
180     IF CVI(O1$)=4 OR CVI(O1$)=5 OR CVI(O2$)=4 OR CVI(O2$)=5 THEN LSET F1$=I$:B=B+1:PUT #3,B
190 NEXT K
195 PRINT"Ф.И. успевающих"
200 FOR I=1 TO B:GET#3,I:PRINT F1$:NEXT I:END     'Вывод на экран
