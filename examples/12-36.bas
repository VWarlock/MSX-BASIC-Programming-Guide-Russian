10 CLS:INPUT"Число городов";N
20 DIM CC[N,N],ST[N,N],G[N]
30 FOR I=1 TO N:FOR J=1 TO N
40 PRINT"Из"I"-го города в"J"-ый:";:INPUT CC[I,J]
50 NEXT J,I
60 PRINT"Это - данная таблица:"
70 FOR I=1 TO N:FOR J=1 TO N:PRINTCC[I,J];TAB(4*J);:NEXTJ:PRINT:NEXTI
80 PRINT"Таблица минимальных цен:"
90 FOR I=1 TO N
100 FOR J=1 TO N:G[J]=J:NEXT J
110 G[1]=I:G[I]=1:S=1
120 FOR J=1 TO N:ST[I,J]=CC[I,J]:NEXT J
130 IF S>=N THEN 210 ELSE FOR R=1 TO 0
140 MIN=ST[I,G[S+1]]:JM=S+1
150 IF S+2>N THEN 170 ELSE FOR J=S+2 TO N
160 IF ST[I,G[J]]<MIN THEN MIN=ST[I,G[J]]:JM=J:NEXT J ELSE NEXT J
170 SWAP G[S+1],G[JM]:S=S+1
180 IF S+1>N THEN 200 ELSE FOR J=S+1 TO N
190 IF ST[I,G[S]]+CC[G[S],G[J]]<ST[I,G[J]] THEN ST[I,G[J]]=ST[I,G[S]]+CC[G[S],G[J]]:NEXT J ELSE NEXT J
200 R=(S<N):NEXT R
210 NEXT I
220 FOR I=1 TO N:FOR J=1 TO N 'Вывод таблицы цен
230 PRINT ST[I,J];TAB(4*J);:NEXT J:PRINT:NEXT I
